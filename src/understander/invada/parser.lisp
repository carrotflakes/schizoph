(defpackage schizoph.invada.parser
  (:use :cl
        :snaky)
  (:export :parse-macro-head
           :parse-body))
(in-package :schizoph.invada.parser)

(defrule macro-head 
    (@ (and str
            (? (and "("
                    ws
                    ident
                    (* (and ws "," ws ident))
                    ws
                    ")")))))

(defrule body
    (or %or
        (ret "")))

(defrule %or
    (or (@ (and (ret :or)
                (or %and (ret :nop))
                (+ (and ws "|" ws
                        (or %and (ret :nop))))))
        %and))

(defrule %and
    (or (@ (and (ret :and)
                bind
                (+ (and ws bind))))
        bind))

(defrule bind
    (or (@ (and (ret :bind)
                factor ws "@" ws ident))
        factor))

(defrule factor
    (or (and "(" ws %or ws ")")
        str
        %*
        %+
        macro-ref))

(defrule %*
    (and "*"
         (ret :*)))

(defrule %+
    (and "+"
         (ret :+)))

(defrule macro-ref
    (@ (and (ret :macro-ref)
            "$"
            ident
            (? (and "("
                    ws
                    body
                    (* (and ws "," ws body))
                    ws
                    ")")))))

(defrule ident
    (grp "ident"
         (cap (+ (and (! (or ws+ (cc "(),|$*+@")))
                      (any))))))

(defrule str
    (mod (@ (+ %char))
         (lambda (chars)
           (format nil "狺汨狎螬┅ㄤ彐蝓戾ャ栳矧ㄡ钿ā矧黧ㄣīお愧┅ㄣ狃ㄡ铢┅ㄡ钿④堍ㄣ狃ㄣīお愧┅┅ㄤ彐蝓戾黧ㄧ蝠Ⅶ栝翦箴徙澧ǐㄣ．ㄦ矧磲铋狺狺幄＼泸＼戽＼翎猢┅┅ㄤ彐蝓戾黧黧┅ㄤ彐疳蝮弪疳蝮瀛磲泸锃桢徜磲泸锃桢徜ㄤ彐疳蝮弪疳蝮瀛怙澌怙澌